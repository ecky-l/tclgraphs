# Commands covered:  graph edge node
#
# This file contains a collection of tests for one or more of the Tcl
# built-in commands.  Sourcing this file into Tcl runs the tests and
# generates output for errors.  No output means no errors were found.
#
# Copyright (c) 2000 by Scriptics Corporation.
#
# See the file "license.terms" for information on usage and redistribution
# of this file, and for a DISCLAIMER OF ALL WARRANTIES.

if {[lsearch [namespace children] ::tcltest] == -1} {
    package require tcltest
    namespace import ::tcltest::*
}
tcltest::configure -verbose {pass}

if {$tcl_platform(platform) eq "windows"} {
    load tclgraphs.dll
}

package require tclgraphs
namespace import tclgraphs::*

#### fixtures
set createThreeNodes {
    graph create g
    node create n1
    node create n2
    node create n3
    set result {}
}
set deleteThreeNodes {
    catch { n1 delete }
    catch { n2 delete }
    catch { n3 delete }
    catch { g delete }
    unset -nocomplain result
}

set createTwoGraphs {
    foreach g {g1 g2} {graph create $g}
    foreach n {n1 n2 n3 n4} {node create $n}
    n1 graphs + g1
    n2 graphs + g1
    n3 graphs + g2
    n4 graphs + g2
}
set deleteTwoGraphs {
    g1 delete -nodes
    g2 delete -nodes
}
#### /fixtures

###

test graph-1.1 "create and delete a graph" -setup {} -body {
    graph create g
    expr { [info command g] != {} }
} -cleanup {
    rename g {}
} -result {1}

###

test graph-1.2 "create graph with name" -setup {} -body {
    graph create g -name "Graf"
    g cget -name
} -cleanup {
    rename g {}
} -result {Graf}

###

test graph-addnode-1.1 "add node to graph via graph" -setup {
    graph create g
    node create n
} -body {
    g nodes + n
    g nodes get
} -cleanup {
    n delete
    g delete
} -result {n}

###

test graph-addnode-1.2 "add node to graph via graph" -setup {
    graph create g
    node create n1
    node create n2
    node create n3
} -body {
    g nodes + n1 n2 n3
    llength [g nodes get]
} -cleanup {
    n1 delete
    n2 delete
    n3 delete
    g delete
} -result {3}


###

test graph-addnode-and-delete-1.3 "delete graph deletes nodes with -nodes flag" -setup {
    graph create g
} -body {
    g nodes + [node new] [node new] [node new]
    lappend result [llength [g nodes get]]
    g delete -nodes
    lappend result [info commands ::tclgraph::Node*]
} -cleanup {
    unset result
} -result {3 {}}

###

test graph-addnode-and-delete-1.4 "delete node from graph" -setup $createThreeNodes -body {
    g nodes + n1 n2 n3
    lappend result [llength [g nodes get]]
    n1 graphs - g
    lappend result [info commands n1] [llength [g nodes get]]
} -cleanup $deleteThreeNodes -result {3 n1 2}

###

test graph-getnodes-1.5.1 "add and get nodes" -setup $createThreeNodes -body {
    g nodes + n1 n2 n3
    lmap x {n1 n2 n3} { expr { $x in [g nodes get] } }
} -cleanup $deleteThreeNodes -result {1 1 1}

###

test graph-getnodes-1.5.2 "add and get nodes label 1" -setup $createThreeNodes -body {
    g nodes + n1 n2 n3
    lmap x {n1 n2 n3} { expr { $x in [g nodes get] } }
} -cleanup $deleteThreeNodes -result {1 1 1}

###

test graph-getnodes-1.5.3 "add and get nodes label 2" -setup $createThreeNodes -body {
    g nodes + n1 n2 n3
    n2 labels + hello world foo bar
    n3 labels + blubb hi
    g nodes get -label world
} -cleanup $deleteThreeNodes -result n2

###

test graph-getnodes-1.5.4 "add and get nodes label 3" -setup $createThreeNodes -body {
    g nodes + n1 n2 n3
    n1 labels + world tu jours
    n2 labels + hello world foo bar
    n3 labels + blubb hi
    g nodes get -notlabel world
} -cleanup $deleteThreeNodes -result n3

###

test graph-getnodes-1.5.5 "add and get nodes label 4" -setup $createThreeNodes -body {
    g nodes + n1 n2 n3
    n2 labels + hello world foo bar
    n3 labels + blubb hi
    g nodes get -notlabel hi world
} -cleanup $deleteThreeNodes -result n1

###

test graph-getnodes-1.5.6 "add and get nodes label 5" -setup $createThreeNodes -body {
    g nodes + n1 n2 n3
    n1 labels + world tu jours
    n2 labels + hello world foo bar
    n3 labels + blubb hi
    g nodes get -notlabel hi world
} -cleanup $deleteThreeNodes -result {}

###

test graph-info-1.6.1 "info nodes 1" -setup $createThreeNodes -body {
    g nodes + n1 n2 n3
    lmap x {n1 n2 n3} { expr { $x in [g info nodes] } }
} -cleanup $deleteThreeNodes -result {1 1 1}

###

test graph-getnodes-1.6.2 "info nodes 2" -setup $createThreeNodes -body {
    g nodes + n1 n2 n3
    n1 labels + world tu jours
    n2 labels + hello world foo bar
    n3 labels + blubb hi
    g info nodes -notlabel hi world
} -cleanup $deleteThreeNodes -result {}

###

test graph-getnodes-1.6.3 "info nodes 3" -setup $createThreeNodes -body {
    g nodes + n1 n2 n3
    n1 labels + world tu jours
    n2 labels + hello world foo bar
    n3 labels + blubb hi
    graph info g nodes -notlabel hi world
} -cleanup $deleteThreeNodes -result {}

###

test graph-delta-1.7.1 "info delta 1" -setup $createTwoGraphs -body {
    edge new n1 -> n3
    graph info g1 delta+
} -cleanup $deleteTwoGraphs -result n3

###

test graph-delta-1.7.2 "info delta 2" -setup $createTwoGraphs -body {
    edge new n1 -> n2
    edge new n1 -> n3
    g1 info delta+
} -cleanup $deleteTwoGraphs -result n3

# cleanup
::tcltest::cleanupTests
return
