# Commands covered:  graph edge node
#
# This file contains a collection of tests for one or more of the Tcl
# built-in commands.  Sourcing this file into Tcl runs the tests and
# generates output for errors.  No output means no errors were found.
#
# Copyright (c) 2000 by Scriptics Corporation.
#
# See the file "license.terms" for information on usage and redistribution
# of this file, and for a DISCLAIMER OF ALL WARRANTIES.

if {[lsearch [namespace children] ::tcltest] == -1} {
    package require tcltest
    namespace import ::tcltest::*
}
tcltest::configure -verbose {body pass}

if {$tcl_platform(platform) eq "windows"} {
    load tclgraphs.dll
}

package require tclgraphs
namespace import tclgraphs::*

test graph-1.1 "create and delete a graph" -setup {} -body {
    tclgraphs::graph g
    expr { [info command g] != {} }
} -cleanup {
    rename g {}
} -result {1}

test graph-1.2 "create graph with name" -setup {} -body {
    tclgraphs::graph g -name "Graf"
    g cget -name
} -cleanup {
    rename g {}
} -result {Graf}

test node-1.1 "create and delete node" -setup {} -body {
    tclgraphs::node n
    info command n
} -cleanup {
    rename n {}
} -result {n}

test node-1.2 "create node with name" -setup {} -body {
    tclgraphs::node n -name "Note"
    n cget -name
} -cleanup {
    rename n {}
} -result {Note}

test graph-addnode-1.1 "add node to graph via graph" -setup {
    tclgraphs::graph g
    tclgraphs::node n
} -body {
    g addnode n
    g nodes
} -cleanup {
    n delete
    g delete
} -result {n}

test graph-addnode-1.1 "add node to graph via graph" -setup {
    tclgraphs::graph g
    tclgraphs::node n1
    tclgraphs::node n2
    tclgraphs::node n3
} -body {
    g addnode n1 n2 n3
    llength [g nodes]
} -cleanup {
    n1 delete
    n2 delete
    n3 delete
    g delete
} -result {3}

test graph-addnode-1.2 "add node to graph via node" -setup {
    tclgraphs::graph g
    tclgraphs::node n
} -body {
    n addtograph g
    g nodes
} -cleanup {
    n delete
    g delete
} -result {n}

test graph-addnode-and-delete-1.1 "add and delete node removes node from graph" -setup {
    tclgraphs::graph g
    tclgraphs::node n1
    tclgraphs::node n2
} -body {
    n1 addtograph g
    n2 addtograph g
    n1 delete
    g nodes
} -cleanup {
    n2 delete
    g delete
} -result {n2}

test graph-addnode-and-delete-1.2 "add and delete node leaves empty graph" -setup {
    tclgraphs::graph g
    tclgraphs::node n1
    tclgraphs::node n2
} -body {
    n1 addtograph g
    n2 addtograph g
    lappend result [llength [g nodes]]
    n1 delete
    n2 delete
    lappend result [g nodes]
} -cleanup {
    g delete
    unset result
} -result {2 {}}

test graph-addnode-and-delete-1.3 "delete graph deletes nodes with -nodes flag" -setup {
    tclgraphs::graph g
} -body {
    namespace import ::tclgraphs::node
    g addnodes [node new] [node new] [node new]
    lappend result [llength [g nodes]]
    g delete -nodes
    lappend result [info commands ::tclgraph::Node*]
} -cleanup {
    unset result
} -result {3 {}}

test graph-addnode-and-delete-1.4 "delete node from graph" -setup {
    tclgraphs::graph g
    tclgraphs::node n1
    tclgraphs::node n2
    tclgraphs::node n3
} -body {
    g addnodes n1 n2 n3
    lappend result [llength [g nodes]]
    n1 delfromgraph g
    lappend result [info commands n1] [llength [g nodes]]
} -cleanup {
    unset result
    g delete -nodes
    n1 delete
} -result {3 n1 2}

tcltest::test edge-create-1.1 "create edge via edge command" -setup {
    tclgraphs::node n1
    tclgraphs::node n2
} -body {
    tclgraphs::edge e -from n1 -to n2
} -cleanup {
    e delete
    n1 delete
    n2 delete
} -result {e}

tcltest::test edge-create-1.2 "create edge through node interface" -setup {
    node n1
    node n2
} -body {
    set e [n1 -> n2 new]
    llength [info command $e] 
} -cleanup {
    $e delete
    n1 delete
    n2 delete
} -result {1}


# cleanup
::tcltest::cleanupTests
return
